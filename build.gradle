import nu.studer.gradle.jooq.JooqEdition

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'nu.studer.jooq' version '9.0'
    id 'org.flywaydb.flyway' version '9.7.0'
}

group = 'digital'
version = '0.0.1-SNAPSHOT'

configurations {
    flywayMigration
}

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.h2database:h2'
    flywayMigration 'com.h2database:h2:2.2.224'
    jooqGenerator 'com.h2database:h2:2.2.224'
    implementation 'org.flywaydb:flyway-core'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

flyway {
    configurations = ['flywayMigration']
    url = 'jdbc:h2:~/data/testdb;AUTO_SERVER=TRUE'
    user = 'sa'
    password = ''
}

jooq {
    edition = JooqEdition.OSS
    version = '3.18.11'
    configurations {
        main {
            generationTool {
                jdbc {
                    driver = 'org.h2.Driver'
                    url = flyway.url
                    user = flyway.user
                    password = flyway.password
                }
                generator {
                    database {
                        name = 'org.jooq.meta.h2.H2Database'
                        includes = '.*'
                    }
                    target {
                        packageName = 'digital.library.generated'
                        directory = 'build/generated-src/jooq/main'
                    }
                    generate {
                        deprecated = false
                        records = false
                        immutablePojos = true
                        fluentSetters = true
                        daos = true
                        relations = true
                    }
                }
            }
        }
    }
}

tasks.named('generateJooq') {
    dependsOn 'flywayMigrate'
}
